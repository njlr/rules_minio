"""Declare runtime dependencies

These are needed for local dev, and users must install them as well.
See https://docs.bazel.build/versions/main/skylark/deploying.html#dependencies
"""

load("@bazel_tools//tools/build_defs/repo:http.bzl", _http_archive = "http_archive")
load("@bazel_tools//tools/build_defs/repo:utils.bzl", "maybe")
load("//minio/private:toolchains_repo.bzl", "PLATFORMS", "toolchains_repo")
load("//minio/private:versions.bzl", "TOOL_VERSIONS")

def http_archive(name, **kwargs):
    maybe(_http_archive, name = name, **kwargs)

# WARNING: any changes in this function may be BREAKING CHANGES for users
# because we'll fetch a dependency which may be different from one that
# they were previously fetching later in their WORKSPACE setup, and now
# ours took precedence. Such breakages are challenging for users, so any
# changes in this function should be marked as BREAKING in the commit message
# and released only in semver majors.
# This is all fixed by bzlmod, so we just tolerate it for now.
def rules_minio_dependencies():
    """Install workspace dependencies"""

    # The minimal version of bazel_skylib we require
    http_archive(
        name = "bazel_skylib",
        sha256 = "6e78f0e57de26801f6f564fa7c4a48dc8b36873e416257a92bbb0937eeac8446",
        urls = [
            "https://github.com/bazelbuild/bazel-skylib/releases/download/1.8.2/bazel-skylib-1.8.2.tar.gz",
            "https://mirror.bazel.build/github.com/bazelbuild/bazel-skylib/releases/download/1.8.2/bazel-skylib-1.8.2.tar.gz",
        ],
    )

    http_archive(
        name = "bazel_lib",
        sha256 = "6fd3b1e1a38ca744f9664be4627ced80895c7d2ee353891c172f1ab61309c933",
        strip_prefix = "bazel-lib-3.0.0",
        url = "https://github.com/bazel-contrib/bazel-lib/releases/download/v3.0.0/bazel-lib-v3.0.0.tar.gz",
    )

    http_archive(
        name = "package_metadata",
        sha256 = "5bd0cc7594ea528fd28f98d82457f157827d48cc20e07bcfdbb56072f35c8f67",
        strip_prefix = "supply-chain-0.0.6/metadata",
        url = "https://github.com/bazel-contrib/supply-chain/releases/download/v0.0.6/supply-chain-v0.0.6.tar.gz",
    )

    http_archive(
        name = "io_bazel_rules_go",
        integrity = "sha256-M6zErg9wUC20uJPJ/B3Xqb+ZjCPn/yxFF3QdQEmpdvg=",
        urls = [
            "https://mirror.bazel.build/github.com/bazelbuild/rules_go/releases/download/v0.48.0/rules_go-v0.48.0.zip",
            "https://github.com/bazelbuild/rules_go/releases/download/v0.48.0/rules_go-v0.48.0.zip",
        ],
    )

    http_archive(
        name = "bazel_gazelle",
        integrity = "sha256-12v3pg/YsFBEQJDfooN6Tq+YKeEWVhjuNdzspcvfWNU=",
        urls = [
            "https://mirror.bazel.build/github.com/bazelbuild/bazel-gazelle/releases/download/v0.37.0/bazel-gazelle-v0.37.0.tar.gz",
            "https://github.com/bazelbuild/bazel-gazelle/releases/download/v0.37.0/bazel-gazelle-v0.37.0.tar.gz",
        ],
    )

########
# Remaining content of the file is only used to support toolchains.
########
_DOC = "Fetch external tools needed for minio toolchain"
_ATTRS = {
    "minio_version": attr.string(
        mandatory = True,
        values = TOOL_VERSIONS.keys(),
        default = "RELEASE.2025-08-13T08-35-41Z",
    ),
    "platform": attr.string(mandatory = True, values = PLATFORMS.keys()),
}

_PLATFORMS = {
    "x86_64-unknown-linux-gnu": "linux-amd64",
}

def _platform_to_url_platform(platform):
    return _PLATFORMS[platform] or platform

def _minio_repo_impl(repository_ctx):
    # https://dl.min.io/client/mc/release/linux-amd64/archive/mc
    url = "https://dl.min.io/client/mc/release/{1}/archive/mc.{0}".format(
        repository_ctx.attr.minio_version,
        _platform_to_url_platform(repository_ctx.attr.platform),
    )
    repository_ctx.download(
        url = url,
        integrity = TOOL_VERSIONS[repository_ctx.attr.minio_version][repository_ctx.attr.platform],
        output = "mc",
        executable = True,
    )
    build_content = """# Generated by minio/repositories.bzl
load("@rules_minio//minio:toolchain.bzl", "minio_toolchain")

minio_toolchain(
    name = "minio_toolchain",
    mc = select({
        "@bazel_tools//src/conditions:host_windows": "minio_tool.exe",
        "//conditions:default": "mc",
    }),
)
"""

    # Base BUILD file for this repository
    repository_ctx.file("BUILD.bazel", build_content)

minio_repositories = repository_rule(
    _minio_repo_impl,
    doc = _DOC,
    attrs = _ATTRS,
)

# Wrapper macro around everything above, this is the primary API
def minio_register_toolchains(name, register = True, **kwargs):
    """Convenience macro for users which does typical setup.

    - create a repository for each built-in platform like "minio_linux_amd64"
    - TODO: create a convenience repository for the host platform like "minio_host"
    - create a repository exposing toolchains for each platform like "minio_platforms"
    - register a toolchain pointing at each platform
    Users can avoid this macro and do these steps themselves, if they want more control.

    Args:
        name: base name for all created repos, like "minio1_14"
        register: whether to call through to native.register_toolchains.
            Should be True for WORKSPACE users, but false when used under bzlmod extension
        **kwargs: passed to each minio_repositories call
    """
    for platform in PLATFORMS.keys():
        minio_repositories(
            name = name + "_" + platform,
            platform = platform,
            **kwargs
        )
        if register:
            native.register_toolchains("@%s_toolchains//:%s_toolchain" % (name, platform))

    toolchains_repo(
        name = name + "_toolchains",
        user_repository_name = name,
    )
